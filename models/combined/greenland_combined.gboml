#TIMEHORIZON
    // time horizon definition
    T = 8760;

#GLOBAL
    // global parameters

    demand_el = import "..\..\data\demand_el.csv";  // GWh(e) - tertiary, industry, residential and railway electricity demand + electricity heating
    //wacc = 0.0441;
    onshore_wacc = 0.0150;
    offshore_wacc = 0.0150;
    hydro_wacc = 0.0421;
    number_years_horizon = T/8760; 
    co2_emission_cost = 0; // 80 * 1/(10**3); // M€/kt(co2)
    e_ens_cost = 3; // M€/GWh
    yearly_grid_cost = 0.0; // FOR PV Templates /!\ should find the real value of this
    // GWh(e) - tertiary, industry, residential and railway electricity demand
    cap_co2 = 0.0; // kt C02


// Belgium Infrasctructure definition
// We base ourselves on the infrastrcuture available in 2030

#NODE LIQUEFIED_METHANE_REGASIFICATION = import LIQUEFIED_METHANE_REGASIFICATION from "..\..\templates\remote_hub_wacc.gboml";
#NODE LIQUEFIED_METHANE_STORAGE_DESTINATION = import LIQUEFIED_METHANE_STORAGE_DESTINATION from "..\..\templates\remote_hub_wacc.gboml";

#NODE ENERGY_DEMAND_BE	
	#VARIABLES
	    external: e_ens[T]; // GWh
    #CONSTRAINTS
        // dual_constraint: e_ens[t] >= 0.0; 
        e_ens[t] >=0;
        // dual_constraint: sum(e_ens[i] for i in [0:T-1]) == 0.0; 
        dual_constraint: e_ens[t] == 0.0; 
    #OBJECTIVES
        min: e_ens[t] * global.e_ens_cost; // M€

// Methane Balance Belgium gas
#HYPEREDGE DESTINATION_METHANE_BALANCE
	#PARAMETERS
        demand = import "..\..\data\demand_ng.csv"; // /!\ Gas Demand Constant // GWh (ch4)
        conversion_factor = 15.31; // GWh/kt of liquefied gas
	#CONSTRAINTS
	    conversion_factor * LIQUEFIED_METHANE_REGASIFICATION.methane[t] == 10 * demand[t];

// Methane Balance Belgium Liquid
#HYPEREDGE DESTINATION_LIQUEFIED_METHANE_BALANCE
	#CONSTRAINTS
	    LIQUEFIED_METHANE_CARRIERS_RREH.liquefied_methane_out[t] + LIQUEFIED_METHANE_STORAGE_DESTINATION.liquefied_methane_out[t] == LIQUEFIED_METHANE_STORAGE_DESTINATION.liquefied_methane_in[t] + LIQUEFIED_METHANE_REGASIFICATION.liquefied_methane[t];

// REMOTE HUB GROENLAND

#NODE OFF_WIND_PLANTS_RREH
    #PARAMETERS
        full_capex = 1800; // MEur/GW --> source = Denmark 2030
        lifetime = 30.0;
        annualised_capex = full_capex * global.offshore_wacc * (1 + global.offshore_wacc)**lifetime / ((1 + global.offshore_wacc)**lifetime - 1); // MEur
        fom = 39; // MEur/year --> source = Denmark 2030
        vom = 0.00389; // MEur/GWh --> source = Denmark 2030
        capacity_factor_off_wind = import "..\..\data\load_factor_woff_GL.csv"; // Dimensionless
        max_capacity = 1000.0; // GW
    #VARIABLES
        internal: capacity;
        external: electricity[T];
    #CONSTRAINTS
        electricity[t] == capacity_factor_off_wind[t] * capacity;
        capacity <= max_capacity;
        capacity >= 0;
        electricity[t] >= 0;
    #OBJECTIVES
        min: global.number_years_horizon * (annualised_capex + fom) * capacity;
        min: vom * electricity[t];

#NODE ON_WIND_PLANTS_RREH
    #PARAMETERS
        full_capex = 1110; // in MEur/GW --> source = Denmark 2030
        lifetime = 30.0;
        annualised_capex = full_capex * global.onshore_wacc * (1 + global.onshore_wacc)**lifetime / ((1 + global.onshore_wacc)**lifetime - 1); // MEur
        fom = 13.4; // MEur/year --> source = Denmark 2030
        vom = 0.00144; // MEur/GWh --> source = Denmark 2030
        capacity_factor_wind = import "../../data/load_factor_won_GL.csv"; // Dimensionless
        max_capacity = 1000.0; // GW
    #VARIABLES
        internal: capacity;
        external: electricity[T];
    #CONSTRAINTS
        electricity[t] == capacity_factor_wind[t] * capacity;
        capacity <= max_capacity;
        capacity >= 0;
        electricity[t] >= 0;
    #OBJECTIVES
        min: global.number_years_horizon * (annualised_capex + fom) * capacity;
        min: vom * electricity[t];

#NODE HYDRO_BASIN_03h_RREH
    #PARAMETERS
        // initial_storage = 35.0 * 1e3; // thousands of cubic meters (TCM) , yearly average (GEUS report)
        inflow_series = import "../../data/inflow_series_03h.csv"; // TCM/h, historical inflow data
        evaporation_rate = 0.0001; // m per day, specific to basin location
        area = 25; // km² Report 2005
        max_storage = 22.5 * 1e3; // (TCM)
        min_storage = 5.0 * 1e3; // TCM, operational minimum
    #VARIABLES
        internal: storage[T];
        external: release[T]; 
        external: spill[T];
    #CONSTRAINTS
        storage[0] == storage[T-1];
        //storage[t+1] == storage[t] + inflow_series[t+1] - release[t+1] - spill[t+1] - (evaporation_rate * area * 1e6 / 24);
        storage[t] <= max_storage;
        storage[t] >= min_storage;
        release[t] + spill[t] <= inflow_series[t] + storage[t-1] - storage[t]; // doesn't work with evaporation rate.. 
        release[t] >= 0;
        spill[t] >= 0;
    // #OBJECTIVES
        // min: 0; // Minimize spillage /!\ Ce n'est pas un coût 

#HYPEREDGE BASIN_TO_HYDRO_03h
    #CONSTRAINTS
    water_balance: HYDRO_BASIN_03h_RREH.release[t] == HYDRO_PLANT_03h_RREH.release[t];

#NODE HYDRO_PLANT_03h_RREH
    #PARAMETERS // for >= 50MW
        full_capex = 1713; // in MEur/GW --> source = IRENA 2000-2021
        lifetime = 100.0;
        annualised_capex = full_capex * global.hydro_wacc * (1 + global.hydro_wacc)**lifetime / ((1 + global.hydro_wacc)**lifetime - 1); // MEur
        fom = 40; // MEur/year
        vom = 0.003; // MEur/GWh
        efficiency = 0.87;
        max_capacity = 0.04; // GW, hydropower resource 2005
    #VARIABLES
        internal: capacity;
        external: electricity[T];
        external: release[T]; // Release is water released to turbine
    #CONSTRAINTS
        electricity[t] == efficiency * release[t];
        electricity[t] <= capacity;
        capacity <= max_capacity;
        capacity >= 0;
        electricity[t] >= 0;
    #OBJECTIVES
        min: global.number_years_horizon * (annualised_capex + fom) * capacity;
        min: vom * electricity[t];


#NODE HYDRO_BASIN_03j_RREH
    #PARAMETERS
        // initial_storage = 35.0 * 1e3; // TCM * 1e6 = m³, yearly average (GEUS report)
        inflow_series = import "../../data/inflow_series_03j.csv"; // TCM/h, historical inflow data
        evaporation_rate = 0.0001; // m per day, specific to basin location
        area = 25; // km² Report 2005
        max_storage = 58.5 * 1e3; // (TCM)
        min_storage = 5.0 * 1e3; // TCM, operational minimum
    #VARIABLES
        internal: storage[T];
        external: release[T]; 
        external: spill[T];
    #CONSTRAINTS
        storage[0] == storage[T-1];
        //storage[t+1] == storage[t] + inflow_series[t+1] - release[t+1] - spill[t+1] - (evaporation_rate * area * 1e6 / 24);
        storage[t] <= max_storage;
        storage[t] >= min_storage;
        release[t] + spill[t] <= inflow_series[t] + storage[t-1] - storage[t]; // doesn't work with evaporation rate.. 
        release[t] >= 0;
        spill[t] >= 0;
    // #OBJECTIVES
        // min: 0; // Minimize spillage /!\ Ce n'est pas un coût 

#HYPEREDGE BASIN_TO_HYDRO_03j
    #CONSTRAINTS
        water_balance: HYDRO_BASIN_03j_RREH.release[t] == HYDRO_PLANT_03j_RREH.release[t];

#NODE HYDRO_PLANT_03j_RREH
    #PARAMETERS // for >= 50MW
        full_capex = 1713; // in MEur/GW --> source = IRENA 2000-2021
        lifetime = 100.0;
        annualised_capex = full_capex * global.hydro_wacc * (1 + global.hydro_wacc)**lifetime / ((1 + global.hydro_wacc)**lifetime - 1); // MEur
        fom = 40; // MEur/year
        vom = 0.003; // MEur/GWh
        efficiency = 0.87;
        max_capacity = 0.099; // GW, hydropower resource 2005
    #VARIABLES
        internal: capacity;
        external: electricity[T];
        external: release[T]; // Release is water released to turbine
    #CONSTRAINTS
        electricity[t] == efficiency * release[t];
        electricity[t] <= capacity;
        capacity <= max_capacity;
        capacity >= 0;
        electricity[t] >= 0;
    #OBJECTIVES
        min: global.number_years_horizon * (annualised_capex + fom) * capacity;
        min: vom * electricity[t];


#NODE HYDRO_BASIN_05h_RREH
    #PARAMETERS
        // initial_storage = 35.0 * 1e3; // TCM * 1e6 = m³, yearly average (GEUS report)
        inflow_series = import "../../data/inflow_series_05h.csv"; // TCM/h, historical inflow data
        evaporation_rate = 0.0001; // m per day, specific to basin location
        area = 25; // km² Report 2005
        max_storage = 35.0 * 1e3; // (TCM)
        min_storage = 5.0 * 1e3; // TCM, operational minimum
    #VARIABLES
        internal: storage[T];
        external: release[T]; 
        external: spill[T];
    #CONSTRAINTS
        storage[0] == storage[T-1];
        //storage[t+1] == storage[t] + inflow_series[t+1] - release[t+1] - spill[t+1] - (evaporation_rate * area * 1e6 / 24);
        storage[t] <= max_storage;
        storage[t] >= min_storage;
        release[t] + spill[t] <= inflow_series[t] + storage[t-1] - storage[t]; // doesn't work with evaporation rate.. 
        release[t] >= 0;
        spill[t] >= 0;
    // #OBJECTIVES
        // min: 0; // Minimize spillage /!\ Ce n'est pas un coût 

#HYPEREDGE BASIN_TO_HYDRO_05h
    #CONSTRAINTS
        water_balance: HYDRO_BASIN_05h_RREH.release[t] == HYDRO_PLANT_05h_RREH.release[t];

#NODE HYDRO_PLANT_05h_RREH
    #PARAMETERS // for >= 50MW
        full_capex = 1713; // in MEur/GW --> source = IRENA 2000-2021
        lifetime = 100.0;
        annualised_capex = full_capex * global.hydro_wacc * (1 + global.hydro_wacc)**lifetime / ((1 + global.hydro_wacc)**lifetime - 1); // MEur
        fom = 40; // MEur/year
        vom = 0.003; // MEur/GWh
        efficiency = 0.87;
        max_capacity = 0.058; // GW, hydropower resource 2005
    #VARIABLES
        internal: capacity;
        external: electricity[T];
        external: release[T]; // Release is water released to turbine
    #CONSTRAINTS
        electricity[t] == efficiency * release[t];
        electricity[t] <= capacity;
        capacity <= max_capacity;
        capacity >= 0;
        electricity[t] >= 0;
    #OBJECTIVES
        min: global.number_years_horizon * (annualised_capex + fom) * capacity;
        min: vom * electricity[t];

#NODE WAVE_PLANT_RREH
    #PARAMETERS
        full_capex = 1701.0; // MEur/GW --> source = Denmark 2050
        lifetime = 30.0;
        annualised_capex = full_capex * global.wacc * (1 + global.wacc)**lifetime / ((1 + global.wacc)**lifetime - 1); // MEur
        fom = 0; // MEur/year --> source = Denmark 2050
        vom = 0.00744; // MEur/GWh --> source = Denmark 2050
        wave_energy_density = import "..\..\data\wave_series_goff.csv"; // GW/m
        // max_space = 60000.0; // m
        max_unit = 10000.0;
        unit_rated_power = 0.0059; // GW
        unit_width = 260.0; // m
        space_between = 10.0; // m, to check
        M = 10000;
    #VARIABLES
        internal integer: num_units;
        internal: length; // m
        internal: init_electricity[T];
        internal: up_bound;
        external: electricity[T]; // GW
        internal binary: z_t[T];
    #CONSTRAINTS
        init_electricity[t] == wave_energy_density[t] * num_units * unit_width;
        up_bound == num_units * unit_rated_power;
        init_electricity[t]-up_bound <= M*z_t[t];
        up_bound-init_electricity[t] <= M*(1-z_t[t]);
        electricity[t] <= up_bound;         
        electricity[t] <= init_electricity[t];
        electricity[t] >= init_electricity[t] - M * z_t[t];
        electricity[t] >= up_bound - M * (1 - z_t[t]);
        length == num_units * (unit_width + space_between);
        electricity[t] >= 0;
        num_units <= max_unit;
        num_units >= 0; 
    #OBJECTIVES
        min: global.number_years_horizon * (annualised_capex + fom) * (unit_rated_power * num_units);
        min: vom * electricity[t];
        
        
#NODE BATTERY_STORAGE_RREH = import BATTERY_STORAGE from "..\..\templates\remote_hub_wacc.gboml";
#NODE HVDC_RREH = import HVDC from "..\..\templates\remote_hub_wacc.gboml" with full_capex_lines = 0.25*100;
#NODE ELECTROLYSIS_PLANTS_RREH = import ELECTROLYSIS_PLANTS from "..\..\templates\remote_hub_wacc.gboml";
#NODE DESALINATION_PLANTS_RREH = import DESALINATION_PLANTS from "..\..\templates\remote_hub_wacc.gboml";
#NODE DIRECT_AIR_CAPTURE_PLANTS_RREH = import DIRECT_AIR_CAPTURE_PLANTS from "..\..\templates\remote_hub_wacc.gboml";
#NODE HYDROGEN_STORAGE_RREH = import HYDROGEN_STORAGE from "..\..\templates\remote_hub_wacc.gboml";
#NODE CARBON_DIOXIDE_STORAGE_RREH = import CARBON_DIOXIDE_STORAGE from "..\..\templates\remote_hub_wacc.gboml";
#NODE WATER_STORAGE_RREH = import WATER_STORAGE from "..\..\templates\remote_hub_wacc.gboml";
#NODE METHANATION_PLANTS_RREH = import METHANATION_PLANTS from "..\..\templates\remote_hub_wacc.gboml";
#NODE METHANE_LIQUEFACTION_PLANTS_RREH = import METHANE_LIQUEFACTION_PLANTS from "..\..\templates\remote_hub_wacc.gboml";
#NODE LIQUEFIED_METHANE_STORAGE_HUB_RREH = import LIQUEFIED_METHANE_STORAGE_HUB from "..\..\templates\remote_hub_wacc.gboml";
#NODE LIQUEFIED_METHANE_CARRIERS_RREH = import LIQUEFIED_METHANE_CARRIERS from "..\..\templates\remote_hub_wacc.gboml";
#HYPEREDGE INLAND_POWER_BALANCE_RREH
    #CONSTRAINTS
        inland_power_balance_gr: OFF_WIND_PLANTS_RREH.electricity[t] + ON_WIND_PLANTS_RREH.electricity[t] + HYDRO_PLANT_03h_RREH.electricity[t] + HYDRO_PLANT_03j_RREH.electricity[t] + HYDRO_PLANT_05h_RREH.electricity[t] + WAVE_PLANT_RREH.electricity[t] + BATTERY_STORAGE_RREH.electricity_out[t] == BATTERY_STORAGE_RREH.electricity_in[t] + HVDC_RREH.electricity_in[t];

#HYPEREDGE COASTAL_POWER_BALANCE_RREH
    #CONSTRAINTS
        HVDC_RREH.electricity_out[t] == ELECTROLYSIS_PLANTS_RREH.electricity[t] + HYDROGEN_STORAGE_RREH.electricity[t] + DESALINATION_PLANTS_RREH.electricity[t] + WATER_STORAGE_RREH.electricity[t] + DIRECT_AIR_CAPTURE_PLANTS_RREH.electricity[t] + CARBON_DIOXIDE_STORAGE_RREH.electricity[t] + METHANE_LIQUEFACTION_PLANTS_RREH.electricity[t];

#HYPEREDGE COASTAL_HYDROGEN_BALANCE_RREH
    #CONSTRAINTS
        ELECTROLYSIS_PLANTS_RREH.hydrogen[t] + HYDROGEN_STORAGE_RREH.hydrogen_out[t] == HYDROGEN_STORAGE_RREH.hydrogen_in[t] + DIRECT_AIR_CAPTURE_PLANTS_RREH.hydrogen[t] + METHANATION_PLANTS_RREH.hydrogen[t];

#HYPEREDGE COASTAL_WATER_BALANCE_RREH
    #CONSTRAINTS
        DESALINATION_PLANTS_RREH.water[t] + METHANATION_PLANTS_RREH.water[t] + WATER_STORAGE_RREH.water_out[t] == WATER_STORAGE_RREH.water_in[t] + ELECTROLYSIS_PLANTS_RREH.water[t] + DIRECT_AIR_CAPTURE_PLANTS_RREH.water[t];


#HYPEREDGE COASTAL_METHANE_BALANCE_RREH
    #CONSTRAINTS
        METHANATION_PLANTS_RREH.methane[t] == METHANE_LIQUEFACTION_PLANTS_RREH.methane[t];


#HYPEREDGE COASTAL_LIQUEFIED_METHANE_BALANCE_RREH
    #CONSTRAINTS
        liquefied_methane_balance: METHANE_LIQUEFACTION_PLANTS_RREH.liquefied_methane[t] + LIQUEFIED_METHANE_STORAGE_HUB_RREH.liquefied_methane_out[t] == LIQUEFIED_METHANE_STORAGE_HUB_RREH.liquefied_methane_in[t] + LIQUEFIED_METHANE_CARRIERS_RREH.liquefied_methane_in[t]; // GWh;

#HYPEREDGE COASTAL_CARBON_DIOXIDE_BALANCE_RREH
    #CONSTRAINTS
        only_carrier: DIRECT_AIR_CAPTURE_PLANTS_RREH.carbon_dioxide[t] + CARBON_DIOXIDE_STORAGE_RREH.carbon_dioxide_out[t] == CARBON_DIOXIDE_STORAGE_RREH.carbon_dioxide_in[t] + METHANATION_PLANTS_RREH.carbon_dioxide[t];
