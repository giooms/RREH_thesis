#TIMEHORIZON
    // time horizon definition
    T = 8760;

#GLOBAL
    // global parameters

    demand_el = import "..\..\data\demand_el.csv";  // GWh(e) - tertiary, industry, residential and railway electricity demand + electricity heating
    wacc = 0.07;
    number_years_horizon = T/8760; 
    co2_emission_cost = 0; // 80 * 1/(10**3); // M€/kt(co2)
    e_ens_cost = 3; // M€/GWh
    yearly_grid_cost = 0.0; // FOR PV Templates /!\ should find the real value of this
    // GWh(e) - tertiary, industry, residential and railway electricity demand
    cap_co2 = 0.0; // kt C02


// Belgium Infrasctructure definition
// We base ourselves on the infrastrcuture available in 2030

#NODE LIQUEFIED_METHANE_REGASIFICATION = import LIQUEFIED_METHANE_REGASIFICATION from "..\..\templates\remote_hub_wacc.gboml";
#NODE LIQUEFIED_METHANE_STORAGE_DESTINATION = import LIQUEFIED_METHANE_STORAGE_DESTINATION from "..\..\templates\remote_hub_wacc.gboml";

#NODE ENERGY_DEMAND_BE	
	#VARIABLES
	    external: e_ens[T]; // GWh
    #CONSTRAINTS
        // dual_constraint: e_ens[t] >= 0.0; 
        e_ens[t] >=0;
        // dual_constraint: sum(e_ens[i] for i in [0:T-1]) == 0.0; 
        dual_constraint: e_ens[t] == 0.0; 
    #OBJECTIVES
        min: e_ens[t] * global.e_ens_cost; // M€

// Methane Balance Belgium gas
#HYPEREDGE DESTINATION_METHANE_BALANCE
	#PARAMETERS
        demand = import "..\..\data\demand_ng.csv"; // /!\ Gas Demand Constant // GWh (ch4)
        conversion_factor = 15.31; // GWh/kt of liquefied gas
	#CONSTRAINTS
	    conversion_factor * LIQUEFIED_METHANE_REGASIFICATION.methane[t] == 10 * demand[t];

// Methane Balance Belgium Liquid
#HYPEREDGE DESTINATION_LIQUEFIED_METHANE_BALANCE
	#CONSTRAINTS
	    LIQUEFIED_METHANE_CARRIERS_GR.liquefied_methane_out[t] + LIQUEFIED_METHANE_STORAGE_DESTINATION.liquefied_methane_out[t] == LIQUEFIED_METHANE_STORAGE_DESTINATION.liquefied_methane_in[t] + LIQUEFIED_METHANE_REGASIFICATION.liquefied_methane[t];

// REMOTE HUB GROENLAND

#NODE WAVE_PLANT_GR
    #PARAMETERS
        full_capex = 1701.0; // MEur/GW --> source = Denmark 2050
        lifetime = 30.0;
        annualised_capex = full_capex * global.wacc * (1 + global.wacc)**lifetime / ((1 + global.wacc)**lifetime - 1); // MEur
        fom = 0; // MEur/year --> source = Denmark 2050
        vom = 0.00744; // MEur/GWh --> source = Denmark 2050
        wave_energy_density = import "..\..\data\wave_series_goff.csv"; // GW/m
        // max_space = 60000.0; // m
        max_unit = 1000.0;
        unit_rated_power = 0.0059; // GW
        unit_width = 260.0; // m
        space_between = 10.0; // m, to check
        M = 10000;
    #VARIABLES
        internal integer: num_units;
        internal: length; // m
        internal: init_electricity[T];
        internal: up_bound;
        external: electricity[T]; // GW
        internal binary: z_t[T];
    #CONSTRAINTS
        init_electricity[t] == wave_energy_density[t] * num_units * unit_width;
        up_bound == num_units * unit_rated_power;
        init_electricity[t]-up_bound <= M*z_t[t];
        up_bound-init_electricity[t] <= M*(1-z_t[t]);
        electricity[t] <= up_bound;         
        electricity[t] <= init_electricity[t];
        electricity[t] >= init_electricity[t] - M * z_t[t];
        electricity[t] >= up_bound - M * (1 - z_t[t]);
        length == num_units * (unit_width + space_between);
        electricity[t] >= 0;
        num_units <= max_unit;
        num_units >= 0; 
    #OBJECTIVES
        min: global.number_years_horizon * (annualised_capex + fom) * (unit_rated_power * num_units);
        min: vom * electricity[t];
        
#NODE BATTERY_STORAGE_GR = import BATTERY_STORAGE from "..\..\templates\remote_hub_wacc.gboml";
#NODE HVDC_GR = import HVDC from "..\..\templates\remote_hub_wacc.gboml" with full_capex_lines = 0.25*100;
#NODE ELECTROLYSIS_PLANTS_GR = import ELECTROLYSIS_PLANTS from "..\..\templates\remote_hub_wacc.gboml";
#NODE DESALINATION_PLANTS_GR = import DESALINATION_PLANTS from "..\..\templates\remote_hub_wacc.gboml";
#NODE DIRECT_AIR_CAPTURE_PLANTS_GR = import DIRECT_AIR_CAPTURE_PLANTS from "..\..\templates\remote_hub_wacc.gboml";
#NODE HYDROGEN_STORAGE_GR = import HYDROGEN_STORAGE from "..\..\templates\remote_hub_wacc.gboml";
#NODE CARBON_DIOXIDE_STORAGE_GR = import CARBON_DIOXIDE_STORAGE from "..\..\templates\remote_hub_wacc.gboml";
#NODE WATER_STORAGE_GR = import WATER_STORAGE from "..\..\templates\remote_hub_wacc.gboml";
#NODE METHANATION_PLANTS_GR = import METHANATION_PLANTS from "..\..\templates\remote_hub_wacc.gboml";
#NODE METHANE_LIQUEFACTION_PLANTS_GR = import METHANE_LIQUEFACTION_PLANTS from "..\..\templates\remote_hub_wacc.gboml";
#NODE LIQUEFIED_METHANE_STORAGE_HUB_GR = import LIQUEFIED_METHANE_STORAGE_HUB from "..\..\templates\remote_hub_wacc.gboml";
#NODE LIQUEFIED_METHANE_CARRIERS_GR = import LIQUEFIED_METHANE_CARRIERS from "..\..\templates\remote_hub_wacc.gboml";
#HYPEREDGE INLAND_POWER_BALANCE_GR
    #CONSTRAINTS
        inland_power_balance_gr: WAVE_PLANT_GR.electricity[t] + BATTERY_STORAGE_GR.electricity_out[t] == BATTERY_STORAGE_GR.electricity_in[t] + HVDC_GR.electricity_in[t];

#HYPEREDGE COASTAL_POWER_BALANCE_GR
    #CONSTRAINTS
        HVDC_GR.electricity_out[t] == ELECTROLYSIS_PLANTS_GR.electricity[t] + HYDROGEN_STORAGE_GR.electricity[t] + DESALINATION_PLANTS_GR.electricity[t] + WATER_STORAGE_GR.electricity[t] + DIRECT_AIR_CAPTURE_PLANTS_GR.electricity[t] + CARBON_DIOXIDE_STORAGE_GR.electricity[t] + METHANE_LIQUEFACTION_PLANTS_GR.electricity[t];

#HYPEREDGE COASTAL_HYDROGEN_BALANCE_GR
    #CONSTRAINTS
        ELECTROLYSIS_PLANTS_GR.hydrogen[t] + HYDROGEN_STORAGE_GR.hydrogen_out[t] == HYDROGEN_STORAGE_GR.hydrogen_in[t] + DIRECT_AIR_CAPTURE_PLANTS_GR.hydrogen[t] + METHANATION_PLANTS_GR.hydrogen[t];

#HYPEREDGE COASTAL_WATER_BALANCE_GR
    #CONSTRAINTS
        DESALINATION_PLANTS_GR.water[t] + METHANATION_PLANTS_GR.water[t] + WATER_STORAGE_GR.water_out[t] == WATER_STORAGE_GR.water_in[t] + ELECTROLYSIS_PLANTS_GR.water[t] + DIRECT_AIR_CAPTURE_PLANTS_GR.water[t];


#HYPEREDGE COASTAL_METHANE_BALANCE_GR
    #CONSTRAINTS
        METHANATION_PLANTS_GR.methane[t] == METHANE_LIQUEFACTION_PLANTS_GR.methane[t];


#HYPEREDGE COASTAL_LIQUEFIED_METHANE_BALANCE_GR
    #CONSTRAINTS
        liquefied_methane_balance: METHANE_LIQUEFACTION_PLANTS_GR.liquefied_methane[t] + LIQUEFIED_METHANE_STORAGE_HUB_GR.liquefied_methane_out[t] == LIQUEFIED_METHANE_STORAGE_HUB_GR.liquefied_methane_in[t] + LIQUEFIED_METHANE_CARRIERS_GR.liquefied_methane_in[t]; // GWh;

#HYPEREDGE COASTAL_CARBON_DIOXIDE_BALANCE_GR
    #CONSTRAINTS
        only_carrier: DIRECT_AIR_CAPTURE_PLANTS_GR.carbon_dioxide[t] + CARBON_DIOXIDE_STORAGE_GR.carbon_dioxide_out[t] == CARBON_DIOXIDE_STORAGE_GR.carbon_dioxide_in[t] + METHANATION_PLANTS_GR.carbon_dioxide[t];
