#TIMEHORIZON
T=43800;

#GLOBAL
wacc = 0.07;
number_years_horizon = T/8760;

#NODE SOLAR_PV_PLANTS
#PARAMETERS
full_capex = 380.0;
lifetime = 25.0;
annualised_capex = full_capex * global.wacc * (1 + global.wacc)**lifetime / ((1 + global.wacc)**lifetime - 1); // MEur
fom = 7.25; // MEur/year
vom = 0.0;
capacity_factor_PV = import "../data/pv_capacity_factors_sahara.csv"; // Dimensionless
max_capacity = 500.0; // GW
#VARIABLES
internal: capacity;
external: electricity[T];
#CONSTRAINTS
electricity[t] <= capacity_factor_PV[t] * capacity;
capacity <= max_capacity;
capacity >= 0;
electricity[t] >= 0;
#OBJECTIVES
min: global.number_years_horizon * (annualised_capex + fom) * capacity;
min: vom * electricity[t];

#NODE WIND_PLANTS
#PARAMETERS
full_capex = 1040.0;
lifetime = 30.0;
annualised_capex = full_capex * global.wacc * (1 + global.wacc)**lifetime / ((1 + global.wacc)**lifetime - 1); // MEur
fom = 12.6; // MEur/year
vom = 0.00135; // MEur/GWh
capacity_factor_wind = import "../data/wind_capacity_factors_sahara.csv"; // Dimensionless
max_capacity = 500.0; // GW
#VARIABLES
internal: capacity;
external: electricity[T];
#CONSTRAINTS
electricity[t] <= capacity_factor_wind[t] * capacity;
capacity <= max_capacity;
capacity >= 0;
electricity[t] >= 0;
#OBJECTIVES
min: global.number_years_horizon * (annualised_capex + fom) * capacity;
min: vom * electricity[t];

#NODE BATTERY_STORAGE
#PARAMETERS
full_capex_stock = 142.0;
full_capex_flow = 160.0;
lifetime_stock = 10.0;
lifetime_flow = 10.0;
annualised_capex_stock = full_capex_stock * global.wacc * (1 + global.wacc)**lifetime_stock / ((1 + global.wacc)**lifetime_stock - 1); // MEur
annualised_capex_flow = full_capex_flow * global.wacc * (1 + global.wacc)**lifetime_flow / ((1 + global.wacc)**lifetime_flow - 1); // MEur
fom_stock = 0.0;
fom_flow = 0.5;
vom_stock = 0.0018;
vom_flow = 0.0;
charge_discharge_ratio = 1.0;
self_discharge = 0.00004;
efficiency_in = 0.959;
efficiency_out = 0.959;
#VARIABLES
internal: capacity_flow;
internal: capacity_stock;
internal: electricity_stored[T];
external: electricity_in[T];
external: electricity_out[T];
#CONSTRAINTS
electricity_in[t] <= capacity_flow;
electricity_out[t] <= charge_discharge_ratio * capacity_flow;
electricity_stored[t] <= capacity_stock;
electricity_stored[0] == electricity_stored[T-1];
electricity_stored[t+1] == (1 - self_discharge) * electricity_stored[t] + efficiency_in * electricity_in[t] - electricity_out[t] / efficiency_out;
capacity_flow >= 0;
capacity_stock >= 0;
electricity_stored[t] >= 0;
electricity_in[t] >= 0;
electricity_out[t] >= 0;
#OBJECTIVES
min: global.number_years_horizon * (annualised_capex_stock + fom_stock) * capacity_stock + global.number_years_horizon * (annualised_capex_flow + fom_flow) * capacity_flow;
min: vom_stock * electricity_stored[t] + vom_flow * electricity_in[t];

#NODE HVDC
#PARAMETERS
full_capex_lines = 0.25*1000;
full_capex_stations = 2*115.0;
lifetime_lines = 40.0;
lifetime_stations = 40.0;
annualised_capex_lines = full_capex_lines * global.wacc * (1 + global.wacc)**lifetime_lines / ((1 + global.wacc)**lifetime_lines - 1); // MEur
annualised_capex_stations = full_capex_stations * global.wacc * (1 + global.wacc)**lifetime_stations / ((1 + global.wacc)**lifetime_stations - 1); // MEur
annualised_capex = annualised_capex_lines + annualised_capex_stations; // MEur/GW-year (Lines + Stations)
fom = 2.5 + 4.6; // MEur/year
vom = 0.0;
efficiency_HVDC = 0.9499;
#VARIABLES
internal: capacity;
external: electricity_in[T];
external: electricity_out[T];
#CONSTRAINTS
electricity_in[t] <= capacity;
electricity_out[t] == efficiency_HVDC * electricity_in[t];
capacity >= 0;
electricity_in[t] >= 0;
electricity_out[t] >= 0;
#OBJECTIVES
min: global.number_years_horizon * (annualised_capex + fom) * capacity;
min: vom * electricity_in[t];

#NODE ELECTROLYSIS_PLANTS
#PARAMETERS
full_capex = 600.0;
lifetime = 15.0;
annualised_capex = full_capex * global.wacc * (1 + global.wacc)**lifetime / ((1 + global.wacc)**lifetime - 1); // MEur
fom = 30.0; // MEur/year
vom = 0.0;
conversion_factor_electricity = 50.6;
conversion_factor_water = 9.0;
minimum_level = 0.05;
#VARIABLES
internal: capacity; // GW - reference flow for sizing is electricity
external: electricity[T]; // GWh/h
external: water[T]; // kt/h
external: hydrogen[T]; // kt/h
#CONSTRAINTS
electricity[t] <= capacity;
minimum_level * capacity <= electricity[t];
electricity[t] == conversion_factor_electricity * hydrogen[t];
water[t] == conversion_factor_water * hydrogen[t];
capacity >= 0;
electricity[t] >= 0;
hydrogen[t] >= 0;
water[t] >= 0;
#OBJECTIVES
min: global.number_years_horizon * (annualised_capex + fom) * capacity;
min: vom * electricity[t];

#NODE DESALINATION_PLANTS
#PARAMETERS
full_capex = 28.08;
lifetime = 20.0;
annualised_capex = full_capex * global.wacc * (1 + global.wacc)**lifetime / ((1 + global.wacc)**lifetime - 1); // MEur
fom = 0.0; // MEur/year
vom = 0.000315;
conversion_factor_electricity = 0.004;
minimum_level = 1.0;
ramp_rate_up = 0.0;
ramp_rate_down = 0.0;
#VARIABLES
internal: capacity; // kt/h - freshwater is the reference flow for sizing
external: electricity[T];
external: water[T];
#CONSTRAINTS
water[t] <= capacity;
minimum_level * capacity <= water[t];
electricity[t] == conversion_factor_electricity * water[t];
water[t] <= water[t-1] + ramp_rate_up * capacity;
water[t-1] <= water[t] + ramp_rate_down * capacity;
capacity >= 0;
electricity[t] >= 0;
water[t] >= 0;
#OBJECTIVES
min: global.number_years_horizon * (annualised_capex + fom) * capacity;
min: vom * water[t];

#NODE DIRECT_AIR_CAPTURE_PLANTS
#PARAMETERS
full_capex = 4801.4;
lifetime = 30.0;
annualised_capex = full_capex * global.wacc * (1 + global.wacc)**lifetime / ((1 + global.wacc)**lifetime - 1); // MEur
fom = 0.0; // MEur/year
vom = 0.0207; // MEur/kt
conversion_factor_electricity = 0.1091;
conversion_factor_water = 5.0;
conversion_factor_hydrogen = 1.46 / 33.3; // heat consumption / LHV of hydrogen
minimum_level = 1.0;
ramp_rate_up = 0.0;
ramp_rate_down = 0.0;
#VARIABLES
external: capacity; // kt/h - carbon dioxide is the reference flow for sizing
external: electricity[T];
external: hydrogen[T];
external: water[T];
external: carbon_dioxide[T];
#CONSTRAINTS
carbon_dioxide[t] <= capacity;
minimum_level * capacity <= carbon_dioxide[t];
electricity[t] == conversion_factor_electricity * carbon_dioxide[t];
water[t] == conversion_factor_water * carbon_dioxide[t];
hydrogen[t] == conversion_factor_hydrogen * carbon_dioxide[t];
carbon_dioxide[t] <= carbon_dioxide[t-1] + ramp_rate_up * capacity;
carbon_dioxide[t-1] <= carbon_dioxide[t] + ramp_rate_down * capacity;
capacity >= 0;
electricity[t] >= 0;
water[t] >= 0;
hydrogen[t] >= 0;
carbon_dioxide[t] >= 0;
#OBJECTIVES
min dac_cost_fix: global.number_years_horizon * (annualised_capex + fom) * capacity;
min dac_cost_var: vom * carbon_dioxide[t];
min dac_permits: -global.co2_emission_cost * carbon_dioxide[t];

#NODE HYDROGEN_STORAGE
#PARAMETERS
full_capex_stock = 45.0;
full_capex_flow = 0.0;
lifetime_stock = 30.0;
lifetime_flow = 30.0;
annualised_capex_stock = full_capex_stock * global.wacc * (1 + global.wacc)**lifetime_stock / ((1 + global.wacc)**lifetime_stock - 1); // MEur
annualised_capex_flow = full_capex_flow * global.wacc * (1 + global.wacc)**lifetime_flow / ((1 + global.wacc)**lifetime_flow - 1); // MEur
fom_stock = 2.25;
fom_flow = 0.0;
vom_stock = 0.0;
vom_flow = 0.0;
conversion_factor_electricity = 1.3;
minimum_level = 0.05;
#VARIABLES
internal: capacity_flow;
internal: capacity_stock;
internal: hydrogen_stored[T];
external: electricity[T];
external: hydrogen_in[T];
external: hydrogen_out[T];
#CONSTRAINTS
hydrogen_in[t] <= capacity_flow;
hydrogen_out[t] <= capacity_flow;
minimum_level * capacity_stock <= hydrogen_stored[t];
hydrogen_stored[t] <= capacity_stock;
hydrogen_stored[0] == hydrogen_stored[T-1];
hydrogen_stored[t+1] == hydrogen_stored[t] + hydrogen_in[t] - hydrogen_out[t];
electricity[t] == conversion_factor_electricity * hydrogen_in[t];
capacity_flow >= 0;
capacity_stock >= 0;
hydrogen_stored[t] >= 0;
hydrogen_in[t] >= 0;
hydrogen_out[t] >= 0;
electricity[t] >= 0;
#OBJECTIVES
min: global.number_years_horizon * (annualised_capex_stock + fom_stock) * capacity_stock + global.number_years_horizon * (annualised_capex_flow + fom_flow) * capacity_flow;
min: vom_stock * hydrogen_stored[t] + vom_flow * hydrogen_in[t];

#NODE CARBON_DIOXIDE_STORAGE
#PARAMETERS
full_capex_stock = 1.35;
full_capex_flow = 32.4+16.2;
lifetime_stock = 30.0;
lifetime_flow = 30.0;
annualised_capex_stock = full_capex_stock * global.wacc * (1 + global.wacc)**lifetime_stock / ((1 + global.wacc)**lifetime_stock - 1); // MEur
annualised_capex_flow = full_capex_flow * global.wacc * (1 + global.wacc)**lifetime_flow / ((1 + global.wacc)**lifetime_flow - 1); // MEur
fom_stock = 0.0675;
fom_flow = 1.62 + 0.81; // MEur/(kt/h)-year (carbon dioxide liquefaction + regasification)
vom_stock = 0.0;
vom_flow = 0.0;
conversion_factor_electricity = 0.105;
#VARIABLES
internal: capacity_flow;
internal: capacity_stock;
internal: carbon_dioxide_stored[T];
external: electricity[T];
external: carbon_dioxide_in[T];
external: carbon_dioxide_out[T];
#CONSTRAINTS
carbon_dioxide_in[t] <= capacity_flow;
carbon_dioxide_out[t] <= capacity_flow;
carbon_dioxide_stored[t] <= capacity_stock;
carbon_dioxide_stored[0] == carbon_dioxide_stored[T-1];
carbon_dioxide_stored[t+1] == carbon_dioxide_stored[t] + carbon_dioxide_in[t] - carbon_dioxide_out[t];
electricity[t] == conversion_factor_electricity * carbon_dioxide_in[t];
capacity_flow >= 0;
capacity_stock >= 0;
carbon_dioxide_stored[t] >= 0;
carbon_dioxide_in[t] >= 0;
carbon_dioxide_out[t] >= 0;
electricity[t] >= 0;
#OBJECTIVES
min: global.number_years_horizon * (annualised_capex_stock + fom_stock) * capacity_stock + global.number_years_horizon * (annualised_capex_flow + fom_flow) * capacity_flow;
min: vom_stock * carbon_dioxide_stored[t] + vom_flow * carbon_dioxide_in[t];

#NODE WATER_STORAGE
#PARAMETERS
full_capex_stock = 0.065;
full_capex_flow = 1.55923;
lifetime_stock = 30.0;
lifetime_flow = 30.0;
annualised_capex_stock = full_capex_stock * global.wacc * (1 + global.wacc)**lifetime_stock / ((1 + global.wacc)**lifetime_stock - 1); // MEur
annualised_capex_flow = full_capex_flow * global.wacc * (1 + global.wacc)**lifetime_flow / ((1 + global.wacc)**lifetime_flow - 1); // MEur
fom_stock = 0.0013;
fom_flow = 0.0312;
vom_stock = 0.0;
vom_flow = 0.0;
conversion_factor_electricity = 0.00036;
#VARIABLES
internal: capacity_flow;
internal: capacity_stock;
internal: water_stored[T];
external: electricity[T];
external: water_in[T];
external: water_out[T];
#CONSTRAINTS
water_in[t] <= capacity_flow;
water_out[t] <= capacity_flow;
water_stored[t] <= capacity_stock;
water_stored[0] == water_stored[T-1];
water_stored[t+1] == water_stored[t] + water_in[t] - water_out[t];
electricity[t] == conversion_factor_electricity * water_in[t];
capacity_flow >= 0;
capacity_stock >= 0;
water_stored[t] >= 0;
water_in[t] >= 0;
water_out[t] >= 0;
electricity[t] >= 0;
#OBJECTIVES
min: global.number_years_horizon * (annualised_capex_stock + fom_stock) * capacity_stock + global.number_years_horizon * (annualised_capex_flow + fom_flow) * capacity_flow;
min: vom_stock * water_stored[t] + vom_flow * water_in[t];

#NODE METHANATION_PLANTS
#PARAMETERS
HHV_CH4 = 15.441;
full_capex = 735.0 * HHV_CH4; // to obtain cost in MEur/(kt/h)
lifetime = 20.0;
annualised_capex = full_capex * global.wacc * (1 + global.wacc)**lifetime / ((1 + global.wacc)**lifetime - 1); // MEur
fom = 29.4 * HHV_CH4; // MEur/year
vom = 0.;
conversion_factor_hydrogen = 0.5;
conversion_factor_water = 2.25;
conversion_factor_carbon_dioxide = 2.75;
minimum_level = 1.0;
ramp_rate_up = 0.0;
ramp_rate_down = 0.0;
#VARIABLES
internal: capacity; // kt/h - reference flow for sizing is methane
external: hydrogen[T];
external: carbon_dioxide[T];
external: methane[T];
external: water[T];
#CONSTRAINTS
methane[t] <= capacity;
minimum_level * capacity <= methane[t];
hydrogen[t] == conversion_factor_hydrogen * methane[t];
carbon_dioxide[t] == conversion_factor_carbon_dioxide * methane[t];
water[t] == conversion_factor_water * methane[t];
methane[t] <= methane[t-1] + ramp_rate_up * capacity;
methane[t-1] <= methane[t] + ramp_rate_down * capacity;
capacity >= 0;
methane[t] >= 0;
hydrogen[t] >= 0;
carbon_dioxide[t] >= 0;
water[t] >= 0;
#OBJECTIVES
min: global.number_years_horizon * (annualised_capex + fom) * capacity;
min: vom * methane[t];

#NODE METHANE_LIQUEFACTION_PLANTS
#PARAMETERS
full_capex = 5913.0;
lifetime = 30.0;
annualised_capex = full_capex * global.wacc * (1 + global.wacc)**lifetime / ((1 + global.wacc)**lifetime - 1); // MEur
fom = 147.825; // MEur/year
vom = 0.0;
conversion_factor_electricity = 0.616;
conversion_factor_methane = 1.0;
minimum_level = 1.0;
ramp_rate_up = 0.0;
ramp_rate_down = 0.0;
#VARIABLES
internal: capacity;
external: electricity[T];
external: methane[T];
external: liquefied_methane[T];
#CONSTRAINTS
liquefied_methane[t] <= capacity;
minimum_level * capacity <= liquefied_methane[t];
electricity[t] == conversion_factor_electricity * liquefied_methane[t];
methane[t] == conversion_factor_methane * liquefied_methane[t];
liquefied_methane[t] <= liquefied_methane[t-1] + ramp_rate_up * capacity;
liquefied_methane[t-1] <= liquefied_methane[t] + ramp_rate_down * capacity;
capacity >= 0;
electricity[t] >= 0;
liquefied_methane[t] >= 0;
methane[t] >= 0;
#OBJECTIVES
min: global.number_years_horizon * (annualised_capex + fom) * capacity;
min: vom * liquefied_methane[t];

#NODE LIQUEFIED_METHANE_STORAGE_HUB
#PARAMETERS
full_capex_stock = 2.641;
full_capex_flow = 0.001;
lifetime_stock = 30.0;
lifetime_flow = 30.0;
annualised_capex_stock = full_capex_stock * global.wacc * (1 + global.wacc)**lifetime_stock / ((1 + global.wacc)**lifetime_stock - 1); // MEur
annualised_capex_flow = full_capex_flow * global.wacc * (1 + global.wacc)**lifetime_flow / ((1 + global.wacc)**lifetime_flow - 1); // MEur
fom_stock = 0.05282;
fom_flow = 0.0;
vom_stock = 0.0;
vom_flow = 0.0;
#VARIABLES
internal: capacity_flow;
internal: capacity_stock;
internal: liquefied_methane_stored[T];
external: liquefied_methane_in[T];
external: liquefied_methane_out[T];
#CONSTRAINTS
liquefied_methane_in[t] <= capacity_flow;
liquefied_methane_out[t] <= capacity_flow;
liquefied_methane_stored[t] <= capacity_stock;
liquefied_methane_stored[0] == liquefied_methane_stored[T-1];
liquefied_methane_stored[t+1] == liquefied_methane_stored[t] + liquefied_methane_in[t] - liquefied_methane_out[t];
capacity_flow >= 0;
capacity_stock >= 0;
liquefied_methane_stored[t] >= 0;
liquefied_methane_in[t] >= 0;
liquefied_methane_out[t] >= 0;
#OBJECTIVES
min: global.number_years_horizon * (annualised_capex_stock + fom_stock) * capacity_stock + global.number_years_horizon * (annualised_capex_flow + fom_flow) * capacity_flow;
min: vom_stock * liquefied_methane_stored[t] + vom_flow * liquefied_methane_in[t];

#NODE LIQUEFIED_METHANE_CARRIERS
#PARAMETERS
number_carriers = 7;
full_capex = 2.537;
lifetime = 30.0;
annualised_capex = full_capex * global.wacc * (1 + global.wacc)**lifetime / ((1 + global.wacc)**lifetime - 1); // MEur/year
fom = 0.12685; // MEur/year
vom = 0.0;
schedule = import "../data/carrier_schedule.csv";
loading_time = 24;
travel_time = 116;
conversion_factor = 0.994;
#VARIABLES
internal: capacity;
external: liquefied_methane_in[T];
external: liquefied_methane_out[T];
#CONSTRAINTS
liquefied_methane_in[t] <= schedule[t] * capacity;
liquefied_methane_out[t+travel_time] == conversion_factor * liquefied_methane_in[t];
liquefied_methane_out[t] == 0 where t < travel_time;
capacity >= 0;
liquefied_methane_in[t] >= 0;
liquefied_methane_out[t] >= 0;
#OBJECTIVES
min: global.number_years_horizon * (annualised_capex + fom) * capacity * loading_time * number_carriers;
min: vom * liquefied_methane_in[t];

#NODE LIQUEFIED_METHANE_STORAGE_DESTINATION
#PARAMETERS
full_capex_stock = 2.641;
full_capex_flow = 0.001;
lifetime_stock = 30.0;
lifetime_flow = 30.0;
annualised_capex_stock = full_capex_stock * global.wacc * (1 + global.wacc)**lifetime_stock / ((1 + global.wacc)**lifetime_stock - 1); // MEur
annualised_capex_flow = full_capex_flow * global.wacc * (1 + global.wacc)**lifetime_flow / ((1 + global.wacc)**lifetime_flow - 1); // MEur
fom_stock = 0.05282;
fom_flow = 0.0;
vom_stock = 0.0;
vom_flow = 0.0;
#VARIABLES
internal: capacity_flow;
internal: capacity_stock;
internal: liquefied_methane_stored[T];
external: liquefied_methane_in[T];
external: liquefied_methane_out[T];
#CONSTRAINTS
liquefied_methane_in[t] <= capacity_flow;
liquefied_methane_out[t] <= capacity_flow;
liquefied_methane_stored[t] <= capacity_stock;
liquefied_methane_stored[0] == liquefied_methane_stored[T-1];
liquefied_methane_stored[t+1] == liquefied_methane_stored[t] + liquefied_methane_in[t] - liquefied_methane_out[t];
capacity_flow >= 0;
capacity_stock >= 0;
liquefied_methane_stored[t] >= 0;
liquefied_methane_in[t] >= 0;
liquefied_methane_out[t] >= 0;
#OBJECTIVES
min: global.number_years_horizon * (annualised_capex_stock + fom_stock) * capacity_stock + global.number_years_horizon * (annualised_capex_flow + fom_flow) * capacity_flow;
min: vom_stock * liquefied_methane_stored[t] + vom_flow * liquefied_methane_in[t];

#NODE LIQUEFIED_METHANE_REGASIFICATION
#PARAMETERS
full_capex = 1248.3; // Mâ‚¬ / (kt/h)
lifetime = 30.0;
annualised_capex = full_capex * global.wacc * (1 + global.wacc)**lifetime / ((1 + global.wacc)**lifetime - 1); // MEur
fom = 24.97; // MEur/year
vom = 0.0;
conversion_factor = 0.98;
#VARIABLES
internal: capacity; // kt/h
external: liquefied_methane[T]; //kt/h
external: methane[T]; // kt/h
#CONSTRAINTS
liquefied_methane[t] <= capacity;
methane[t] == conversion_factor * liquefied_methane[t]; // kt/h
capacity >= 0;
methane[t] >= 0;
liquefied_methane[t] >= 0;
#OBJECTIVES
min: global.number_years_horizon * (annualised_capex + fom) * capacity;
min: vom * liquefied_methane[t];

#HYPEREDGE INLAND_POWER_BALANCE
#CONSTRAINTS
SOLAR_PV_PLANTS.electricity[t] + WIND_PLANTS.electricity[t] + BATTERY_STORAGE.electricity_out[t] == BATTERY_STORAGE.electricity_in[t] + HVDC.electricity_in[t];

#HYPEREDGE COASTAL_POWER_BALANCE
#CONSTRAINTS
HVDC.electricity_out[t] == ELECTROLYSIS_PLANTS.electricity[t] + HYDROGEN_STORAGE.electricity[t] + DESALINATION_PLANTS.electricity[t] + WATER_STORAGE.electricity[t] + DIRECT_AIR_CAPTURE_PLANTS.electricity[t] + CARBON_DIOXIDE_STORAGE.electricity[t] + METHANE_LIQUEFACTION_PLANTS.electricity[t];

#HYPEREDGE COASTAL_HYDROGEN_BALANCE
#CONSTRAINTS
ELECTROLYSIS_PLANTS.hydrogen[t] + HYDROGEN_STORAGE.hydrogen_out[t] == HYDROGEN_STORAGE.hydrogen_in[t] + DIRECT_AIR_CAPTURE_PLANTS.hydrogen[t] + METHANATION_PLANTS.hydrogen[t];

#HYPEREDGE COASTAL_WATER_BALANCE
#CONSTRAINTS
DESALINATION_PLANTS.water[t] + METHANATION_PLANTS.water[t] + WATER_STORAGE.water_out[t] == WATER_STORAGE.water_in[t] + ELECTROLYSIS_PLANTS.water[t] + DIRECT_AIR_CAPTURE_PLANTS.water[t];

#HYPEREDGE COASTAL_CARBON_DIOXIDE_BALANCE
#CONSTRAINTS
DIRECT_AIR_CAPTURE_PLANTS.carbon_dioxide[t] + CARBON_DIOXIDE_STORAGE.carbon_dioxide_out[t] == CARBON_DIOXIDE_STORAGE.carbon_dioxide_in[t] + METHANATION_PLANTS.carbon_dioxide[t];

#HYPEREDGE COASTAL_METHANE_BALANCE
#CONSTRAINTS
METHANATION_PLANTS.methane[t] == METHANE_LIQUEFACTION_PLANTS.methane[t];

#HYPEREDGE COASTAL_LIQUEFIED_METHANE_BALANCE
#CONSTRAINTS
METHANE_LIQUEFACTION_PLANTS.liquefied_methane[t] + LIQUEFIED_METHANE_STORAGE_HUB.liquefied_methane_out[t] == LIQUEFIED_METHANE_STORAGE_HUB.liquefied_methane_in[t] + LIQUEFIED_METHANE_CARRIERS.liquefied_methane_in[t];

#HYPEREDGE DESTINATION_LIQUEFIED_METHANE_BALANCE
#CONSTRAINTS
LIQUEFIED_METHANE_CARRIERS.liquefied_methane_out[t] + LIQUEFIED_METHANE_STORAGE_DESTINATION.liquefied_methane_out[t] == LIQUEFIED_METHANE_STORAGE_DESTINATION.liquefied_methane_in[t] + LIQUEFIED_METHANE_REGASIFICATION.liquefied_methane[t];

// #HYPEREDGE DESTINATION_METHANE_BALANCE
// #PARAMETERS
// demand = import "data/REB/gas_demand.csv";
// #CONSTRAINTS
// LIQUEFIED_METHANE_REGASIFICATION.methane[t] == demand[t];
